
\section{LowerA Combinator}
\label{chap3:lower}

% explain how lowerA is used and the effect of lowerA
% explain input filter function and related type class
% explain lowerA implementation
% restriction in lowerA

Combinator \co{lowerA} is introduced in FlowHaskellRef to alleviate the restriction posed by \co{pure}. 
It downgrades the output security label of a \co{FlowArrowRef} to an expected level. An input filtering 
mechanism is provided to guarantee no information leak due to unauthorized downgrading of security label.
The following figure illustrates how \co{lowerA} works:
\begin{verbatim}
A diagram similar in the presentation slide 
showing when input filtering and output 
security label deduction happened.
\end{verbatim}
%
% Explain the diagram.
%
When an undefined value is used somewhere in the program, the whole program
aborts immediately and no information can leak.

\subsection{Input Filtering Mechanism}
The input filtering mechanism in \co{lowerA} is implemented by 
method \co{removeHigh} in type class \co{SecFilter}. Any input type of \co{FlowArrowRef} which is used in 
\co{lowerA} requires an instance in the type class \co{SecFilter}. The implementations for integer, pair,
and reference are as following(in SecureFlow.hs):
\begin{verbatim}
class (Lattice l) => SecFilter s l t where
  removeHigh :: l -> (s l) -> t -> t

instance (Lattice l) => SecFilter SecType l Int where
  removeHigh l (SecLabel l') t = 
               if label_leq l' l then t else undefined

instance (Lattice l, SecFilter SecType l a
         , SecFilter SecType l b) =>
           SecFilter SecType l (a,b) where
  removeHigh l (Pair lx ly) (x,y) = 
               (removeHigh l lx x, removeHigh l ly y)

instance (Lattice l, SecFilter SecType l a) =>
           SecFilter SecType l (SecRef a) where
  removeHigh l (Ref t l') (MkSecRef r fin) = 
               if label_leq l' l
                 then (MkSecRef r ((removeHigh l t).fin))
                 else undefined ))
\end{verbatim}
Method \co{removeHigh} takes a lattice label \co{l}, a complex security label (\co{s~l}), and a value of 
type \co{t}. In the instance of \co{Int}, the security label is compared with \co{l}. If it is higher 
than \co{l}, \co{undefined} is returned. Otherwise, the same value is returned. This is the same for any 
data type that is protected by a lattice label.
When \co{removeHigh} applied to a pair, \co{removeHigh} is applied to both its first and second elements
with the corresponding security labels and values.
For reference, \co{removeHigh} compares the identity security label, $l'$, with expected security label, $l$. 
If $l'$ is higher than $l$, undefined is returned. Otherwise, \co{removeHigh} should applied to the content
of the reference. But even if the security label of the content is higher than $l$, the content of the 
reference cannot become undefined because it may be shared by other references. The undefined value should
only be observed if the content is read out from the reference in discuss. All other references pointed to
the same content should be read normally. That is the reason that a {\em projection}
function is introduced in \co{SecRef}. A partial evaluated function $(\cod{removeHigh}~l~t)$, which takes
a value and returns either the same value or undefined, is composed with the original projection function,
\cod{fin}. By this way, if the security label of the content is too high, only the result of reading this
reference becomes undefined.

\subsection{Deduction of Output Security Label}
Function $\rho$ in Figure~\ref{fig:deduce} is implemented by method \co{low} in type class \co{Downgrade}. 
Type class \co{Donwgrade} deduces security labels for certain data type based on a dummy value of
that type.
The definition of \co{Downgrade} and some instances are illustrated as following(in SecureFlow.hs): 
\begin{verbatim}
class (Lattice l) => Downgrade s l t where
  low :: l -> t -> (s l)

instance (Lattice l) => Downgrade SecType l Int where
  low l t = (SecLabel (Lab l))

instance (Lattice l, Downgrade SecType l a
         , Downgrade SecType l b) =>
           Downgrade SecType l (a,b) where
  low l (x,y) = (Pair (low l x) (low l y))

instance (Lattice l, Dummy a, Downgrade SecType l a) =>
            Downgrade SecType l (SecRef a) where
  low l (MkSecRef _ _) = (Ref (low l (dummy::a)) (Lab l))
\end{verbatim}
Type variable \co{s} is a complex security type based on a lattice \co{l}.
Method \co{low} takes a lattice label and a value of type \co{t}, and returns a corresponding security label 
for data of type \co{t} with every label annotation being \co{l}.
In the instance of \co{Int}, a \co{SecLabel} with label annotation \co{l} is returned. 
In the instance of pair, a constructor \co{Pair} is returned with method \co{low} applying again to both 
elements of the pair.
For reference type, a constructor \co{Ref} with label $l$ as identity security label is returned. 
Function \co{low} is applied again to deduce the security label of the content given a dummy value which has 
the same type as the content, \co{a}.
Operator \co{dummy} belongs to type class \co{Dummy} and returns a value of type \co{a} in this case. 

The output type of a \co{FlowArrowRef} is recorded in the type signature. 
Type class \co{DowngradeArrow} is introduced to collect output types from
the type signatures of {\em arrows}(in SecureFlow.hs).
\begin{verbatim}
class (Lattice l, Arrow a) =>
      DowngradeArrow s l a b c where
  lowFlow :: l -> (a b c) -> (s l, s l)

instance (Lattice l, Downgrade SecType l b
         , Downgrade SecType l c
         , Dummy b, Dummy c, Arrow a) =>
         DowngradeArrow SecType l a b c where
  lowFlow l t = ((SecVar "x0"), low l (dummy::c))
\end{verbatim}
Type variable \co{s} is a complex security type and variables \co{b} and \co{c} are types of input and output
of an {\em arrow} \co{a}.
Method \co{lowFlow} takes a lattice label and an {\em arrow}, and return a pair of security labels in which
the first and the second elements are security labels for input and output, respectively. In the instance 
of \co{SecType}, \co{lowFlow} returns a unification variable as input security label. The output security
label is generated by method \co{low}, given an expected lattice label \co{l} and a dummy value of type \co{c}.

\subsection{Implementation of \co{lowerA}}
The implementation of \co{lowerA} is as following(in FlowArrowRef.hs):
\begin{verbatim}
lowerA :: (Lattice l, Arrow ar,
         Downgrade SecType l a, Downgrade SecType l b,
         Dummy a, Dummy b, SecFilter SecType l a,
         DowngradeArrow SecType l (FlowArrowRef l ar) a b)
        =>
          l -> FlowArrowRef l ar a b -> FlowArrowRef l ar a b
lowerA level total@(FARef (FA f flow' cons' gcons' uniset') 
                          pc' consRef' gconsRef') =
 if (hasVarSecType pc') || (not (pc' ==  SecLabel (Lab label_top)))
   then error $ "Only toppest side effect allowed in lowerA."
   else
   let (in_flow, out_flow) = (lowFlow level total) in
   let inputFilter upd = 
           pure (\i -> (removeHigh level (upd in_flow) i)) in
   FARef { flowarrow = FA { computation = (\upd -> (inputFilter upd) 
                                                   >>> (f upd))
                          , flow = Trans in_flow out_flow
                          , constraints = []
                          , gconstraints = []
                          , uniset = []
                          }
         , pc = SecLabel (Lab label_top)
         , constraintsRef = []
         , gconstraintsRef = [GLLEQS (SecLabel (Lab level)) in_flow]
         }
\end{verbatim}
Combinator \co{lowerA} takes a lattice label, \co{level}, and a \co{FlowArrowRef}, \co{total}.
In the first line of the implementation, only computation with \co{pc'} being explicit $\top$ can 
be accepted by \co{lowerA}. Because \co{lowerA} expects the input security labels that are lower than
or equal to \co{level}, the lower bound of side effect should be higher than \co{level}. However,
an input security variable is required in the input filtering mechanism, and field \co{flow} is defined
by the new variables. This may lead to unification failure for the variables in the original \co{FlowArrowRef}.
Therefore, all constraints and unification set that might contain variables are abandoned, and \co{pc'}
must be $\top$. A \co{FlowArrowRef} created by \co{pure} is still applicable in the approximation made.

Method \co{lowFlow} is applied to an expected lattice label, \co{level}, and a \co{FlowArrowRef}, \co{total},
and returns new input and output security labels, which are \co{in\_flow} and \co{out\_flow}, respectively. 
Function \co{inputFilter} defines the input filter function via \co{pure}. The function lifted by \co{pure}
takes an argument and pass it along with \co{level} and the input security label \co{in\_flow} to
function \co{removeHigh}.
However, \co{in\_fow} is a variable and can only be resolved to a value after unification. 
Moreover, during the construction of a \co{FlowArrowRef} program, \co{in\_flow} may be renamed several times 
in other combinators of \co{FlowArrowRef}. 
Thus, a substitution function, called \co{upd}, is introduced as a parameter of \co{inputFilter}. 
A substitution function takes a security label and returns a new security label which is similar to the input but
with some variables inside replaced by other security labels. Function \co{upd} is applied to input security
label \co{in\_flow} and the result is a security label without variables. Then, method \co{removeHigh} can
execute as expected. Function \co{inputFilter} is sequenced with the computation in the original \co{FlowArrowRef}.
Because the substitution function is known only from outside a computation, the definition
of field \co{computation} is adapted to take a substitution function and returns an underlying arrow computation,
as described in Sec.~\ref{chap4:unification:flowarrowref}.

The substitution function is constructed in {\em arrow} combinators. The following is an example of combinator
(\arrowop{>})(in FlowArrowRef.hs):
\begin{verbatim}
a1@(FA c1 f1 t1 g1 u1) >>> a2@(FA c2 f2 t2 g2 u2) =
     let (sub1, sub2, _) = (make_sub a1 a2 []) in
     let (f,c,u) = flow_seq (replace_flow sub1 f1) 
                            (replace_flow sub2 f2) in
      FA { computation = 
             (\upd -> c1 (upd.(replaceSecType sub1)) >>>
                      c2 (upd.(replaceSecType sub2)))
           ...
         }

replaceSecType :: Lattice l => [(SecType l, SecType l)] 
                  -> SecType l -> SecType l
\end{verbatim}
Function \co{make\_sub} collects all variables in \co{FlowArrowRef} \co{a1} and \co{a2}, and returns 
substitutions \co{sub1} and \co{sub2}, which a substitution corresponds to a variable renaming. 
Function \co{replaceSecType} takes a substitution and becomes
a substitution function described in the paragraph above. In field \co{computation}, a new substitution 
function, such as (\co{replaceSecType} \co{sub1}), which records the renaming happened in this combinator 
is composed with the substitution function
passed from outside. By this substitution function composition, a variable inside a computation 
performs all renaming in the same order as other variables in other fields.

In the type system of \co{lowerA}, a constraint $\ell\lleqs \sts{1}$ is generated to ensure all label 
annotations of the input security label are higher than or equal to the expected lattice label, $\ell$.
The following program illustrates the necessity of the constraint:
\begin{verbatim}
lowerA H (pure (\r -> r))
\end{verbatim}
Assume a lattice has two labels, \co{L} and \co{H}, and $\cod{L}\sqsubseteq \cod{H}$. If a reference 
of security type $\cod{L}~\mathbf{ref}^{\cod{L}}$ is passed to the program above. The output 
security label becomes $\cod{H}~\mathbf{ref}^{\cod{H}}$. Then some value of security label \co{H} can
be written to the reference. However, if the content is shared by other references, the value of 
security label \co{H} can be read out as a value of security label \co{L} with those references, and
information is leaked.


